model{

      for( j in 1:NbEvents ) {
        for (i in pos[j]:(pos[j+1] -1)){ 
        
        ## Likelihood ##
          M[i] ~ dnorm(mu[i], prec.obs[i])
          mu[i] ~ dnorm(theta[j], prec.date[i])
          prec.obs[i] <- 1/(s[i]*s[i])
          
        # Introduction of a random effect : use of the Shrinkage distribution
        # this help handling potential outliers
          u[i] ~ dunif(0,1)
          prec.date[i] <- 1/s02[j] *u[i]/(1-u[i])
          sd[i] <- 1/sqrt(prec.date[i])
        
        }
      } 
      
  ## Prior distribution of the dates of interest ##   
  for (i in 1:NbEvents) { 
    tt[i] ~ dunif(ta,tb) 
  }
  theta = sort(tt) 
  
}
